amethyst Normalize{
  must_empty = .:e &{must_empty?(e)} {e}
	cant_fail  = .:e &{cant_fail?(e) } {e}

  or = Or[ ( Or[ .* @ary:[ary] ]
           | Apply[ ["fails"] ]
           | Placeholder .* {Apply["empty"]}:[ary]  
					 | Seq[(Cut .* | cant_fail)* eof {@self}:[ary] ] .*
           | .:[ary]
           )* ]
          {ary.size}=>( 0 -> Apply["fails"]
                      | 1 -> ary[0]
                      | . -> Or.create({:ary=>ary})
                      )
        

  seq2 = Seq[ ( Seq[ .* @ary:[ary] ]
              | Apply[ ["fails"] @self:[ary] ] .*
              | Placeholder
              | .:[ary] 
              )* ]
             {ary.size}=>( 0 -> Placeholder
                         | 1 -> ary[0]
                         | . -> Seq.create({:ary=>ary})
                         )
           
  bind = Bind[ {@name}:name
             ( Apply[ ["fails"] {@self} ]
             | switch[ {[]}:ary [.:p .:a {ary<<[p,Bind[name,a]]} ]* {@self.class[{:ary=>ary}]} ] 
             | Seq[.+:ary] {Seq[ary[0...-1]]}:first {ary[-1]}:last
               {last}=>( (Comment|Cut|Stop) -> Seq[Bind[name,first],last]
                       | .                  -> Seq[first,Bind[name,last]]
                       )
             | . -> @self
             ) ]       

  
  switch = Switch_Char | Switch_Clas
         | Or[.*:ary] -> Switch_Or.create(:ary=>ary.map{|k| [nil,k]})


  apply2 = Apply[ ["apply" | "_seq"],Act[Lambda[.:x]] -> x
                | ["apply"]         ,CAct[.:name] .*:args -> Apply[name,*args,{:clas=> resolvegrammar($current_grammar_name,name)}] 
                | ["_seq"]          ,CAct[ [] ]  -> Placeholder
                | .* {@self.freeze}
                ]

  act =  Act[ String[("[]"                  
                     |  number                    
                     |  <A-Z> <A-Za-z0-9_>* &{eval(@self).is_a?(Class)}
                     |  "true"|"false"|"nil"
                     | '"'  (<"> break| '\\' . | .)*
                     | '\'' (<'> break| '\\' . | .)*
                     )  
                    "" ] -> CAct[eval(@ary[0])]
            | Lambda[ .* {Act.create(@self,{:pure=>true}).freeze }]
            | Act | CAct | Local
            ]
}

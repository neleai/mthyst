makeclasses(Object,
    [:Args,:o,:c,:r],
    [:Key,:name,:expr,:args],
    [:Resul,:name,:expr,:args,:vars],
    [:Enter,:name,:cls,:expr],
    :Apply,
    :Nested,
    :Exp,
    :Foreign,
    [:Set,:name,:expr,:append],
    [:Many,:o],
    :Many1,
    :Comment,
    [:Act,:uses,:pred],
    :Lookahead,
    :Not,
    :Seq,
    :Or,
    :And,
		[:Strin],
		[:Variable,:number],
		[:Pass,:var,:to],
    [:Enter,:klas],
    [:Rule,:name,:args,:locals,:body],
    [:Grammar,:name,:parent,:rules],
		:Local
)

def quote(s)
  s=s*""
  '"'+s.gsub('\\"','"').gsub('"','\\"')+'"'
end

$av=0
def autovar
	$av+=1
	Variable[{:ary=>["autovar"],:number=>$av}]
end

def _Enter(to ,from=Apply["anything"],enter=true)
#  And[_Set("it",from), Enter[{:to=>to,:enter=>enter,:var=>Variable["it"]}]]
Enter[to]
end
def _Pass(from,to)
#  _Enter(to,from,false)
	enter=false
	a=autovar
	And[_Set(a,from), Pass[{:to=>to,:enter=>enter,:var=>a}]]
end


def _Set(name,expr,append=false)
  Set[{:name=>name,:expr=>expr,:append=>append}]
end
def _Append(name,expr)
  _Set(name,expr,true)
end

def _Many(expr,many1=false)
  a=autovar
  And[{:ary=>( [_Set(a, _Act("[]"))]+(many1 ? [_Append(a,expr)] : [])+[Many[{:ary=>[_Append(a,expr)],:o=>autovar}],_Act(a)])}]
end
def _Many1(expr)
  _Many(expr,true)
end

def _Act(expr,pred=false)
  Act[{:ary=>[expr],:pred=>pred}]
end
def _Pred(expr)
  _Act(expr,true)
end
def _body(body)
	And[_Set("_result",body), _Act("_result")]
end

$varhash=Hash.new{|h,k| h[k]=Variable[{:ary=>[k],:number=>1}]}
def _Variable(name)
 $varhash[name]
end
amethyst AmethystParser < Amethyst{
	igrammar = ((~'amethyst' .)+:{it*""} | grammar)*:a -> a

	grammar = "amethyst" name:name ("<" name:parent |empty {parent="Amethyst"} ) 
"{" rule*:rules "}"  @Grammar
	
  rule = name:name ruleargs:args "=" expression:body  {_body(body)}:body  @Rule

	expression = choice
	
	choice = listOf('sequence',`"|"`):ary @Or
	
	sequence =  (nr prefixed)*:ary  @And

  prefixed   = "~" modifier:m -> Not[m]
             | "&" inlineHostExpr:expr  -> _Pred(expr)
             | "&" ~inlineHostExpr:expr  modifier:m -> Lookahead[m]
             | modifier:from "=>" modifier:to ->_Pass(from,to)
						 | modifier

 		modifier = term:t optIter(t):t  (~space binding(t):t)* -> t
             | binding(Apply["anything"])
             | "#" (~endline .)*:c -> Comment[c]

   optIter(t) = "*"  -> _Many(t)
              | "+"  -> _Many1(t)
              | "?"   -> Or[t,Apply["empty"]]
              | empty -> t
  
	binding(exp) = {exp}:expr ":" (key | name):name ('[]' {true} | empty {false} ):append ->_Set(name,expr,append)
               | ":" inlineHostExpr:e -> And[ _Set("it",exp) , _Act(e) ]


	term  = (className | "" {"Object"}):cls '[' expression:expr "]" -> And[Apply["clas",cls], _Enter(expr)]
				| className:cls -> Apply["is_a",cls]
				| "nested" '(' expression:expr ")" -> Nested[expr]
		 	  | application
 			  | key
			  | ("->" atomicHostExpr | "" inlineHostExpr):x    ->  _Act(x)
				| "\"" (~'"' eChar)*:s '"' -> Apply["token" ,quote(s)]
		 	  | "'"  (~'\''  eChar)*:s '\''  -> Apply["seq",quote(s)]
 			  | number:{Apply["exactly",it]}
      	| "<" (~">" eChar)+:x  ">"                            ->  Apply["regch","/[#{x}]/"]
		    | "(" expression:x ")[]"															collect(x)
				| "(" expression:x ")"                                ->  x

	application = name:klas '::' name:rule argsOpt('(',')'):arg @Foreign
  	      		| name:rule argsOpt('(',')'):arg -> Apply[rule,arg]

	key         = "@" className:name  argsOpt('[',']'):args  @Resul
              | "@" name:name   @Key

  collect(ors) = -> a=autovar; And[Or[{:ary=>ors.ary.map{|ands| And[{:ary=>ands.ary.map{|expr| _Append(a,expr)}}]}}],_Act(a)]

	eChar = '\\' char:c   -> "\\#{c}"
  	  	| _args('#{','}')
				| char

	ruleargs = argsOpt('(',')')
	
	nr = ~(name ruleargs "=")
	
	argsOpt(o,c) = args(o,c) 
                | empty -> []

	args(o,c) = __args(o,c):r {
							r<< ','
							ary=[]
							tmp=[]
							r.each{|a|
								if a==','
									ary<<Args[{:ary=>tmp}];tmp=[]
								else
									tmp<<a
								end
							}
							ary
						}

	_args(o,c) = __args(o,c):r -> Args[{:ary=>([o]+r+[c])}]

	__args(o,c) = seq(o) "" rubyarg*:r seq(c) -> r
		
	rubyarg  = '`' expression:e '`' -> Exp[_body(e)]
           | _args('(',')')					 | _args('[',']')					 | _args('{','}')
					 | '"' (~'"'   eChar)*:s '"'  -> Strin[['"']+ s+['"']]
					 | '\''(~'\''  eChar)*:s '\'' -> Strin[['\'']+s+['\'']]
					 | ( <$.:>:n[] | empty) <a-zA-Z_>+:n[] -> n*""
					 | '@@' <a-zA-Z_>+:n[] -> Local[n*""]
					 | key:k -> Key[k] 
					 | <^`{}()'"\[\]>

	nameFirst      = <_$.^> | letter

  nameRest       = nameFirst | digit

  className      = _* (upper nameRest* )[]:{leterize(it*"")}
	
	name = _* (nameFirst nameRest*)[]:{leterize(it*"")}

	inlineHostExpr = args('{','}')

	number = <0-9>+

	atomicHostExpr = (~endline .)*:s   {"{#{s*""}}"}=>[ inlineHostExpr ]
}

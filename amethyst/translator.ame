amethyst AmethystTranslator < Amethyst {	
	itrans =  (char | trans)*:r  -> r*""
	
	trans = Grammar[rw('return',`@name:name @parent:parent @rules=>transs:body`)] -> "class #{name} < #{parent}\n#{body}\nend\n"
				| Rule[{$av=0} @name:name @locals:locals @args=>args:argss @body=>trans:body] -> "def #{name}(#{argss})\n #{body} \nend\n"
				| Enter[trans:expr] failwrap("_enter{#{expr}}")
				| Or[rw('next', `transfn*:ors`)]  failwrap("_or(#{ors*","})")
				| And[trans*:t ] -> "#{t*"\n"} "
				| Not[rw('next',`trans:t`) ]  failwrap("_not{#{t}}")
				| Pred[args:t ]  failwrap("(#{t})||FAIL")
				| Act[args:t ] -> t
  			| Lookahead[rw('next',`trans:t`) ]  failwrap("_lookahead{#{t}}")
        | Comment[:c] -> ""
				| Many[rw('break', `trans:t` ) @o=>arg:o] -> "while true\n#{o}=@input;r=#{t}\n break FAIL if r==FAIL\nend;@input=#{o}"
				| Apply[ :name args:argss]  failwrap("#{name}(#{argss})")
				| Foreign[@klas:klas @rule=>rule @args=>args:argss] failwrap("_foreign(#{klas}).#{rule}(#{argss})")
				| Set[@name=>arg:name @expr=>trans:expr @append:append] -> append ? "it=(#{expr})\n #{name}||=[];_append(#{name},it)" : "#{name} = (#{expr})"
				| Resul[@name:name @args=>args:argss @vars:vars] -> "#{name}[#{argss} {#{vars.map{|l| ":#{l[0]}=>#{l[0]}_#{l.number}" }*","} }]"
				| Key[@name:name] -> "_key(:#{name})"
				| Pass[@to=>trans:to @var=>arg:var] failwrap("_pass(#{var}){#{to}}")

	transfn = trans:t -> "proc{#{t}}" 
	
	transs = [ trans*:t ] -> t
	
	args = Array[arg*:a] -> a*","
       |  arg*:a -> a*","
	
	arg = Args[arg*:a] -> a*""
			| Strin[arg*:a] -> a*""
			| Exp[transfn:t ] -> t
		  | ExpKey[ trans:t] -> t
			| Variable[:name @number:number] -> "#{name}_#{number}"
			| anything
				
	rw(word,prc) = {rwo=@@returnword;@@returnword=word} apply(prc):x {@@returnword=rwo;x}

	failwrap(s) = {  "(it=(#{s});#{@@returnword} FAIL if it==FAIL;it)" }
}
